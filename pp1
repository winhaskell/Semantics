%baseclass-preinclude "semantics.h"
%lsp-needed

%token ONE TWO THREE DOZEN 
%token AFEW
%token FOUR FIVE 
%token BURGER SANDWICH MUFFIN CAKE
%left PLUS

%union {
    food* food_info;
    int amount;
}

%type<food_info> food;
%type<amount> amount;

%%

start:
    food
    {
        if(HAS_TO_BE_GLUTEN_FREE && !$1->gluten_free) {
            error("The menu you described may contain gluten!");
        }
        if($1->calories > 2000) {
            error("That's a lot of calories, you cannot eat that much!");
        }
        if($1->type == sweet) {
            std::cout << "OK, that's gonna be " << $1->calories << " sweet calories!" << std::endl;
        }
        else if($1->type == salty) {
            std::cout << "OK, that's gonna be " << $1->calories << " salty calories!" << std::endl;
        }
        else {
            std::cout << "OK, that's gonna be " << $1->calories << " balanced calories!" << std::endl;
        }
    }
;

food:
    amount BURGER
    {
        $$ = new food($1*500, salty, false);
    }
|
    amount SANDWICH
    {
        $$ = new food($1*200, salty, true);
    }
|
    amount MUFFIN
    {
        $$ = new food($1*100, sweet, false);
    }
|
    amount CAKE
    {
	if($1 > 1)
	{
	    error("You cannot eat more than one cake at once!");
	}
        $$ = new food($1*500, sweet, false);
    }
|
    food PLUS food
    {
        if(($1->type == sweet || $1->type == mixed) && $3->type == salty) {
            error("Really? No, you should not eat salty after the sweet...");
        }
        if($1->type == $3->type) {
            $$ = new food($1->calories + $3->calories, $1->type, $1->gluten_free && $3->gluten_free);
        }
        else {
            $$ = new food($1->calories + $3->calories, mixed, $1->gluten_free && $3->gluten_free);
        }
    }
;

amount:
    ONE     { $$ = 1; }
|
    TWO     { $$ = 2; }
|
    THREE   { $$ = 3; }
|
    AFEW   { $$ = 3; }
|
    FOUR   { $$ = 4; }
|
    FIVE   { $$ = 5; }
|
    DOZEN   { $$ = 12; }
;
